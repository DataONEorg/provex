#maxint=20.

%%%%%%%%%%%%%%%%% Manage States %%%%%%%%%%%%%%%

% The INITIAL state
initial(0).

% STATE(S) holds if S is the initial state or a state that's changing
state(S) :-
        initial(S).
state(S1) :-
        changing(S),
        S1 = S + 1.

% NEXT(State, NextState)
next(S,S1) :-
        state(S),
        changing(S),
        S1 = S + 1.

changing(S) :-
        delete_dep(_,_,S).
changing(S) :-
        insert_dep(_,_,S).

final(S) :-
        state(S),
        not changing(S).

%%%%%%%%%%%%%%%%% Set Inputs %%%%%%%%%%%%%%

actor(G,G) :- 
	abstract(_,G).

s_abstract(X,Y,S) :- 
	abstract(X,Y), 
	S=0.

is_lineage :- 
	lineage(_).

all_lineage :- 
	not is_lineage. 

%%%%%%%%%%%%%%%%% Base IDBs %%%%%%%%%%%%%%%

e(X,Y,S) :- 
	used(X,Y), 
	initial(S).
e(X,Y,S) :- 
	gen_by(X,Y), 
	initial(S).

l_node(D,T) :- 
	data(D,_), 
	T="d".
l_node(A,T) :- 
	actor(A,_), 
	T="i".

l_dep(X,Y,S) :- 
	lineage(X), 
	e(X,Y,S). 
l_dep(Y,Z,S) :- 
	l_dep(X,Y,S), 
	e(Y,Z,S).

l_dep(X,Y,S) :- 
	all_lineage, 
	e(X,Y,S).


%%%%%%%%%%%%%%%%% Frame Rules %%%%%%%%%%%%%%%

delete_dep(X,Y,S) :- 
	hide_edge(X,Y), 
	l_dep(X,Y,S), S=0.
delete_dep(X,Y,S) :- 
	hide_node(X), 
	l_dep(X,Y,S), S=0.
delete_dep(X,Y,S) :- 
	hide_node(Y), 
	l_dep(X,Y,S), S=0.

delete_dep(X,Y,S) :- 
	s_abstract(X,G,S), 
	l_dep(X,Y,S).
delete_dep(X,Y,S) :- 
	s_abstract(Y,G,S), 
	l_dep(X,Y,S).

insert_dep(G,Y,S) :- 
	s_abstract(X,G,S), 
	l_dep(X,Y,S),
	not s_abstract(Y,G,S).
insert_dep(X,G,S) :- 
	s_abstract(Y,G,S), 
	l_dep(X,Y,S),
	not s_abstract(X,G,S).

l_dep(X,Y,S1) :- 
	l_dep(X,Y,S), 
	next(S,S1), 
	not delete_dep(X,Y,S).
l_dep(X,Y,S1) :- 
	insert_dep(X,Y,S), 
	next(S,S1).

%%%%%%%%%%%%%%%%% IC Rules %%%%%%%%%%%%%%%

tcdep(X,Y,S) :- 
	l_dep(X,Y,S).
tcdep(X,Y,S) :- 
	l_dep(X,Z,S), 
	tcdep(Z,Y,S).

% Cycle
pv(X,Y,S) :- 
 	tcdep(X,Y,S), 
 	tcdep(Y,X,S), 
 	X!=Y. 
 
% false structure
pv(X,Y,S) :- 
 	l_dep(X,Y,S), 
 	l_node(X,T), 
 	l_node(Y,T).
 
% write conflict	
pv(X,Y,S) :- 
	l_dep(X,Y,S),
	l_dep(X,Z,S),
	l_node(X,TD), TD="d", 
	l_node(Y,TI), TI="i",
	l_node(Z,TI),
	not Y=Z. 	
  
%%%%%%%%%%%%%%%%% Fixes for IC violations %%%%%%%%%%%%%%%

s_abstract(Y,X,S) :- 
	pv(X,Y,S), 
	abstract(_,X). 
s_abstract(X,Y,S) :- 
	pv(X,Y,S), 
	abstract(_,Y). 
s_abstract(X,X,S) :- 
	s_abstract(_,X,S).

viz_data(A,B,C,D) :- 
	l_node(A,_), 
	meta_data(A,B,C,D).
viz_data(A,B,C,D) :- 
	l_node(A,_), 
	meta_actor(A,B,C,D).
